---
page_title: "transcend_data_silo_discovery_plugin Resource - terraform-provider-transcend"
subcategory: ""
description: |-
  
---

# transcend_data_silo_discovery_plugin (Resource)



## Example Usages

### Connecting an AWS Silo Plugin

```terraform
resource "transcend_data_silo" "aws" {
  type        = "amazonWebServices"
  description = "Amazon Web Services (AWS) provides information technology infrastructure services to businesses in the form of web services."

  # Normally, Data Silos are connected in this resource. But for AWS, we want to delay connecting until after
  # we create the IAM Role, which must use the `aws_external_id` output from this resource. So instead, we set
  # `skip_connecting` to `true` here and use a `transcend_data_silo_connection` resource below
  skip_connecting = true
  lifecycle { ignore_changes = [plaintext_context, data_silo_discovery_plugin] }
}

data "aws_caller_identity" "current" {}
resource "transcend_data_silo_connection" "connection" {
  data_silo_id = transcend_data_silo.aws.id

  plaintext_context {
    name  = "role"
    value = "TranscendAWSIntegrationRole"
  }

  plaintext_context {
    name  = "accountId"
    value = "590309927493"
  }

  plaintext_context {
    name  = "region"
    value = "eu-west-1"
  }
}

resource "transcend_data_silo_discovery_plugin" "plugin" {
  data_silo_id = transcend_data_silo.aws.id

  enabled                    = true
  schedule_frequency_minutes = 120
  schedule_start_at          = "2122-09-06T17:51:13.000Z"

  depends_on = [transcend_data_silo_connection.connection]
}

output "plugin_info" {
  value = transcend_data_silo_discovery_plugin.plugin
}
```

The above example shows how you can use this resource to setup a plugin after a silo has been connected via the `transcend_data_silo_connection` resource

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `data_silo_id` (String) The ID of the data silo to connect
- `schedule_frequency_minutes` (Number) The updated frequency with which we should schedule this plugin, in milliseconds
- `schedule_start_at` (String) The updated start time when we should start scheduling this plugin, in ISO format

### Optional

- `enabled` (Boolean) State to toggle plugin to

### Read-Only

- `id` (String) The ID of this resource.
- `last_enabled_at` (String) The date at which this data silo was last enabled

### Connecting discovered data silos

It will take some time for silo discovery to take place. Once it has, and there are some recommendations to add as data silos, please use [the Admin Dashboard's Silo Discovery Triage view](https://app.transcend.io/data-map/data-inventory/silo-discovery/triage) to add as many recommendations to the data inventory as you'd like.

Once that step is complete, you can use this provider to add connection information or plugin information to each data silo.

```terraform
data "transcend_data_silo" "silo" {
  title = "Amazon S3 - sample-bucket-4-transcend-io - from AD"
  discoveredby = transcend_data_silo_discovery_plugin.plugin.data_silo_id
}
```

In this example, we use the `transcend_data_silo` data source to lookup a specific silo discovered by the plugin we setup above.

We could then use the `data.transcend_data_silo.silo.id` output value, representing the ID of the new data silo, in any resource that accepts it, such as `resource_data_point`, `resource_schema_discovery_plugin`, `data_silo_connection`, or others.

## Import

Import is supported using the following syntax:

```shell
terraform import transcend_data_silo_discovery_plugin.connection <data_silo_id_from_silo_url>
```